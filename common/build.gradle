// vim: set expandtab sts=4 sw=4 ai:
description = "Candlepin Common"

apply plugin: Msgfmt
apply plugin: Gettext

msgfmt {
    resource "${project.group}.common.i18n.Messages"
}

// Add the source files generated by the msgfmt plugin
// TODO make the msgfmt plugin add this sourceset to the project directly
sourceSets {
    main {
        java {
            srcDirs "${project.buildDir}/msgfmt/generated_source"
        }
    }
}

// Exclude the generated i18n translation files from checkstyle
tasks.withType(Checkstyle) {
    exclude "**/i18n/Messages*"
}

dependencies {
    implementation libraries.collections
    implementation libraries.commons
    implementation libraries.guice
    implementation libraries.jackson
    implementation libraries.javax
    implementation libraries.liquibase
    implementation libraries.logging_deps
    implementation libraries.oauth
    implementation libraries.resteasy
    implementation libraries.spring
    implementation libraries.hibernate_validator_ap
    implementation libraries.gettext
    implementation libraries.javax_servlet
    implementation libraries.javax_validation
    implementation libraries.validator

    testImplementation libraries.core_testing
    testImplementation libraries.liquibase_slf4j
    testImplementation libraries.jmock
}

task pom {
    doLast {
        conf2ScopeMappings.addMapping(MavenPlugin.COMPILE_PRIORITY + 1, configurations.implementation, 'compile')
        pom {
            project {
                name "Candlepin Common"
                description "Common code for Candlepin"
                parent {
                    groupId "org.candlepin"
                    artifactId "candlepin-parent"
                    version "2.0"
                    relativePath ".."
                }
                packaging "jar"
                build([:]) {
                    plugins {
                        plugin {
                            artifactId "maven-surefire-plugin"
                        }
                        plugin {
                            artifactId "maven-assembly-plugin"
                        }
                        plugin {
                            artifactId "maven-compiler-plugin"
                        }
                        plugin {
                            groupId "com.googlecode.gettext-commons"
                            artifactId "gettext-maven-plugin"
                        }
                        plugin {
                            groupId "org.owasp"
                            artifactId "dependency-check-maven"
                        }
                    }
                }
            }
        }.withXml {
            dependencyManagement.pomConfigurer.configurePom(asNode())
        }.writeTo("pom.xml")
    }
}
